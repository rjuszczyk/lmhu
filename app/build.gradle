import com.android.build.gradle.internal.dsl.SigningConfig

apply plugin: 'com.android.application'
//apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'


static SigningConfig getFromFile(File keystorePropertiesFile) throws FileNotFoundException {

    if(!keystorePropertiesFile.exists()) {
       return error(new FileNotFoundException("No keystore properties for this build variant, file " + keystorePropertiesFile.absolutePath + " not found."))
    }

    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    def storeName = keystoreProperties['storeName']
    def keyAlias = keystoreProperties['keyAlias']
    def keyPassword = keystoreProperties['keyPassword']
    def storeFile = new File(keystorePropertiesFile.parentFile, keystoreProperties['storeFile'])
    if(!storeFile.exists()) {
        return error(new FileNotFoundException(storeFile.absolutePath  + " specified in "+keystorePropertiesFile.absolutePath+ " does not exists"))
    }
    def storePassword = keystoreProperties['storePassword']

    SigningConfig result = new SigningConfig(storeName)

    result.setStoreFile(storeFile)
    result.setStorePassword(storePassword)
    result.setKeyAlias(keyAlias)
    result.setKeyPassword(keyPassword)

    return result
}

static SigningConfig error(Exception exception) {
    return new SigningConfig("error") {
        @Override
        File getStoreFile() {
            throw exception
        }

        @Override
        boolean isSigningReady() {
            return true
        }
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "eu.letmehelpu.android"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    lintOptions {
        disable 'DuplicatePlatformClasses'
    }

    File keystoreDir = rootProject.file("keystore")
    buildTypes {
        release {
            signingConfig getFromFile(new File(keystoreDir, "keystore-release.properties"))
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig getFromFile(new File(keystoreDir, "keystore-debug.properties"))
            debuggable true
        }
        dev {
            initWith debug
            signingConfig getFromFile(new File(keystoreDir, "keystore-dev.properties"))
            applicationIdSuffix ".dev"
        }
    }
}



dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation ('com.facebook.android:facebook-login:4.31.0') {
        exclude group: 'com.android.support', module: 'cardview-v7'
        exclude group: 'com.android.support', module: 'customtabs'
        exclude group: 'com.android.support', module: 'support-media-compat'

    }
    implementation "com.android.support:support-v4:28.0.0-rc01"
    implementation "com.android.support:support-media-compat:28.0.0-rc01"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    implementation 'com.android.support:cardview-v7:28.0.0-rc01'
    implementation 'com.android.support:customtabs:28.0.0-rc01'
    implementation 'com.android.support:appcompat-v7:28.0.0-rc01'
    implementation 'com.android.support:design:28.0.0-rc01'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.7.1'


    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"
    implementation 'android.arch.paging:runtime:1.0.1'


    // Dependencies for the REST API example
//    implementation("com.google.api-client:google-api-client:1.23.0") {
//        exclude group: "com.google.guava", module: "guava-jdk5"
//    }
//    implementation 'com.google.api-client:google-api-client-android:1.23.0'
//    implementation 'com.google.apis:google-api-services-people:v1-rev237-1.23.0'

//    implementation ("com.google.firebase:firebase-core:16.0.1") {
//        exclude module: 'support-media-compat:'
//    }
//    implementation 'com.google.firebase:firebase-messaging:17.1.0'
//    implementation 'com.google.firebase:firebase-firestore:17.1.0'

    // rx java
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.17'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    androidTestCompile 'org.mockito:mockito-android:2.7.22'

    // UiAutomatorTesting
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0-rc01'

    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    compile 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation  "com.google.dagger:dagger:${project.ext.daggerVersion}"
    implementation "com.google.dagger:dagger-android:${project.ext.daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${project.ext.daggerVersion}"
    kapt "com.google.dagger:dagger-compiler:${project.ext.daggerVersion}"
    kapt "com.google.dagger:dagger-android-processor:${project.ext.daggerVersion}"

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation ("com.google.firebase:firebase-core:16.0.3") {
        exclude group: 'com.google.guava'
    }
    implementation 'com.google.firebase:firebase-messaging:17.3.0'
    implementation ("com.google.firebase:firebase-firestore:17.1.0") {
        exclude module: 'support-media-compat:'
    }

    implementation 'com.google.firebase:firebase-auth:16.0.3'
    implementation 'com.google.android.gms:play-services-auth:16.0.0'
}

apply plugin: 'com.google.gms.google-services'